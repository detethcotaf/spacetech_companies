# Fungible Token の実装

## Flow における FT

Flow の FT は他の言語と異なる実装になる。

1. 所有権が分離されている

- Solidity の場合は、トランザクションが発行されるごとに、中央の ERC20 コントラクトとインタラクションし、関数を呼び出す
- Cadence の場合は、ユーザーのアカウントに保存されたコントラクトを使ってトランザクションを実行する

2. Vault リソースを定義して FT の数量を取り扱う

Vault とは FT を保存する金庫のようなイメージ。Vault をリソースとして定義し、残高を表す balance を増減することで、
token を表現している。

残高を引き出すための withdraw()関数と残高を追加するための deposit()関数を Vault リソースのメソッドとして定義する方法が一般的。

3. Capability を使って Vault へのアクセスをセキュアにする

- Valut を作成したら、FT をやり取りするために、Vault を作成したユーザー以外のその他ユーザーも deposit したり、balance を参照する必要がある
- ただし、withdraw は Vault の所有者のみに制限しないとその他ユーザーが自由に balance を引き出せてしまう
- そこで Capability を使い、deposit()関数の実行と balance の参照のみを公開することで、FT のセキュアなやり取りを実現できる

4. インターフェースを使ってコントラクトの実行をセキュアにする

- コントラクトの関数に対して、リソースのインターフェースとして pre(事前条件)と post(事後条件)を組み込み、assert による状態のチェックをすることができる
- このインターフェースを外部からアクセスできるようにすることで、アカウントの管理者以外は必ずインターフェースに定義した assert が実行されることになる

# Flow における FT の実装

## コントラクトの全体像

- token 総量の定義（totalSupply）
- Vault のインターフェースの定義
- Vault リソースの定義
  - 変数や初期化処理
  - withdraw 関数の定義
  - deposit 関数の定義
- Vault を生成する関数の定義（createVault）
- token を新たに mint するためのリソースの定義（VaultMinter)
- コントラクトデプロイ時に稼働する init()処理
  - Vault を生成する関数を使い、Vault を生成
  - アカウントのストレージに保存

## コントラクトの実装

### コントラクトの定義

```cadence
pub contract ExampleToken {
    pub var totalSupply: UFix64

    // 省略
}
```

- ExampleToken という名前でコントラクトを定義
- 最初に totalSuppy として token の総量を表す変数を定義

### Vault のインターフェースの定義

- Vault リソースの定義の前にインターフェースを定義する（リソース定義時に指定するため）
- ここでは withdraw()関数に post によるアサートを組み込み、deposit()関するに pre によるアサートを組み込んでいる
- また balance の状況もインターフェース経由で確認できるようにしてある

```cadence
// withdraw()のインターフェース定義
// tokenをwithdrawするときに必ず稼働するプログラムになる
pub resource interface Provider {
    pub fun withdraw(amount: UFix64): @Vault {
      post {
        result.balance == amount: "引き出す金額は、このVaultの残高と同じでなければなりません"
      }
    }
}

// deposit()のインターフェース定義
// tokenをdepositするときに必ず稼働するプログラムになる
pub resource interface Receiver {
    pub fun deposit(from: @Vault) {
      pre {
        from.balance > UFix64(0): "預ける残高は 0 より大きくなければなりません"
      }
    }
}

// 参照したい変数をインターフェースとして定義
pub resource interface Balance {
    pub var balance: UFix64
}
```

### Vault リソースの定義

```cadence
    // interfaceを最初に指定する
    pub resource Vault: Provider, Receiver, Balance {
        // 残高を表す
        // 負の数の概念は使わないので、残高には符号なし固定小数を使う
        pub var balance: UFix64

        // balanceの初期化
        init(balance: UFix64) {
            self.balance = balance
        }

        // withdraw
        // 自分の残高から引数のamount分を差し引きし、
        // 引いた分のbalanceを持つVaultを新たに生成し返す
        pub fun withdraw(amount: UFix64): @Vault {
            self.balance = self.balance - amount
            // ここではマイナスになってしまうことを考慮したコーディングになっていないが、
            // Flowにはオーバーフロー、アンダーフローの保護機能が組み込まれており、開発者は気にする必要がない
            return <-create Vault(balance: amount)
        }

        // deposit
        // 受け取ったfromのbalanceを残高に追加
        // 受け取ったfromは破棄する
        pub fun deposit(from: @Vault) {
            self.balance = self.balance + from.balance
            destroy from
        }
    }
```

- Vault リソースの生成はコントラクト内でしか定義できない
- そのため、管理アカウント以外が Vault を作りたい時は、次の createEmptyVault()のように関数化して pub にする必要がある

### Vault を生成する関数の定義

- Vault を生成する処理を初期値と合わせて関数化
- Vault リソースの生成はコントラクト内でしか定義できない制約があるため、関数化して外部からも利用できるようにしている
- 最初は空っぽの balance の状態で Vault を作りたい時に利用

```cadence
    // Vault残高の初期値を0として生成
    pub fun createEmptyVault(): @Vault {
        return <-create Vault(balance: 0)
    }
```

### token を新たに mint するためのリソースの定義

- Vault リソースを新たに mint する（Valut の総量を増やす）ためのリソースを定義する

新しい ExαmpleToken の token を mint し、引数で渡したアカウントの Vault に入金する

- Receiver のインターフェースを実装しているリソースの deposit 関数を使っている
- AnyResource 型になっているため、`ExampleToken.Receiver`のインターフェースを持っていればどんなリソースでも指定可能
- ExampleToken の token 総量を追加し、その token を引数で渡したリソース(recipient)に deposit する、という内容

```cadence
pub resource VaultMinter {
    pub fun mintTokens(amount: UFix64, recipient: Capability<&AnyResouce{Receiver}>) {
        let recipientRef = recipient.borrow()
            ?? panic("Could not borrow a receiver reference to the vault")

        ExampleToken.totalSupply = ExampleToken.totalSupply + UFix64(amont) // 自身のコントラクトの変数は コントラクト名.変数名 で利用する
        recipient.deposit(from: <-create Vault(blance: amount))
    }
}
```

- VaultMinter がリソースなのはなぜ？
- リソースではなく関数として定義した場合以下のデメリットが発生する
  - mintTokens()を直接コントラクトに定義すると、pub をつけている場合、誰でも mint できてしまう
  - pub をつけなかった場合は、mint するためのトランザクションに毎回コントラクト管理者の署名が必要になってしまう（分権化できていない）
- リソースとして定義した場合は
  - VaultMinter をリソースとして移動するだけで mint 権限を与えることができる
  - リソースを Capability 化すること（後述の init 処理に記載）で、必要な権限のみを利用させることができる
- つまり、リソースモデル ＋ Capability は、アクセス制御をコードで定義するのではなく、言語構造として処理してくれるため、安全

- 例えば、0x02 が他のアカウントにトークンの mint を許可したい場合、VaultMinter オブジェクトを他のアカウントに移動するか、他のアカウントに VaultMinter に対する Capability を与えることで実現できる
  - ここの具体例が欲しい、、、イメージがまだ湧いてない
- また、デプロイ後にトークンの mint をさせたくない場合は、コントラクトの初期化時にすべてのトークンを mint し、コントラクトに VaultMinter を含めないようにすれば OK

### コントラクトデプロイ時に稼働する init()処理

```cadence
    // デプロイ時に一度だけ実行される
    init() {
        self.totalSupply = 30.0

        // Vaultを生成し、アカウントのストレージに保存
        let vault <- create Vault(balance: self.totalSupply)
        self.account.save(<-vault, to: /storage/CadenceFungibleTokenTutorialVault)

        // mintするためのリソースを生成し、ストレージに保存
        self.account.save(<-create VaultMinter(), to: /storage/CadenceFungibleTokenTutorialMinter)

        // VaultMinterへのCapabilityをprivateストレージに保存
        // これでアカウントのオーナーだけがVaultMinterへの参照を利用できる
        self.account.link<&VaultMinter>(/private/Minter, target: /storage/CadenceFungibleTokenTutorialMinter)
    }
}
```

- ストレージの path 名はコントラクトごとに共通利用になるため、競合する可能性がある
- そのため、十分にユニークな名称をつける必要がある

### コントラクトコードの全体像

```cadence
// ExampleToken.cdc
pub contract ExampleToken {
    pub var totalSupply: UFix64

    pub resource interface Provider {
        pub fun withdraw(amount: UFix64): @Vault {
            post {
                result.balance == UFix64(amount):
                    "Withdrawal amount must be the same as the balance of the withdrawn Vault"
            }
        }
    }

	  pub resource interface Receiver {
        pub fun deposit(from: @Vault) {
            pre {
                from.balance > 0.0:
                    "Deposit balance must be positive"
            }
        }
    }

    pub resource interface Balance {
        pub var balance: UFix64
    }

    pub resource Vault: Provider, Receiver, Balance {
        pub var balance: UFix64

        init(balance: UFix64) {
            self.balance = balance
        }

        pub fun withdraw(amount: UFix64): @Vault {
            self.balance = self.balance - amount
            return <-create Vault(balance: amount)
        }

        pub fun deposit(from: @Vault) {
            self.balance = self.balance + from.balance
            destroy from
        }
    }

    pub fun createEmptyVault(): @Vault {
        return <-create Vault(balance: 0.0)
    }

    pub resource VaultMinter {
        pub fun mintTokens(amount: UFix64, recipient: Capability<&AnyResource{Receiver}>) {
            let recipientRef = recipient.borrow()
                ?? panic("Could not borrow a receiver reference to the vault")
            ExampleToken.totalSupply = ExampleToken.totalSupply + UFix64(amount)
            recipientRef.deposit(from: <-create Vault(balance: amount))
        }
    }

    init() {
        self.totalSupply = 30.0

        let vault <- create Vault(balance: self.totalSupply)
        self.account.save(<-vault, to: /storage/CadenceFungibleTokenTutorialVault)

        self.account.save(<-create VaultMinter(), to: /storage/CadenceFungibleTokenTutorialMinter)

        self.account.link<&VaultMinter>(/private/Minter, target: /storage/CadenceFungibleTokenTutorialMinter)
    }
}
```

## Capability の実装

### Capability の作成

- 生成した Vault リソースのメソッドを外部から使えるようにするために、Capability を生成する
  - 例えば、別のアカウントがトークンを deposit したり、balance を参照したりできるようにする
- ただ、Capability にわざわざせずに、transaction に署名して使えるようにする、ではダメなのか？（使えるメソッドにだけ pub をつければ良いのでは？）

  - pub をつけないと、別のアカウントが ExampleVault コントラクトを使って Vault を生成できなくなる（つまり別のアカウントは自分の Vault から withdraw したりできなくなる）
  - Capability はあくまでコントラクトを使って mint した Vault への 参照なので、アカウントのストレージへの参照になる
  - コントラクトはインスタンスになる前のクラスのようなもので、オンチェーン上にある

- インターフェースを型として指定して Capability を作成する
- public ストレージに格納することで、 0x02 ユーザー以外も使えるようになる
- 下記の例ではコントラクトの Receiver に定義されている deposit()関数の実行と、Balance に定義されている balance 変数の参照ができるようになる
- post では問題なく Capability が生成されたかどうかのテストをしている

```cadence
import ExampleToken from 0x02

transaction {
    prepare(acct: AuthAccount) {
        // ReceiverとBalanceインターフェースのみを参照できるCapabilityを作成
        acct.link<&ExampleToen.Vault{ExampleToken.Receiver, ExampleToken.Balance}>(/public/CadenceFungibleTokenTutorialReceiver, target: /storage/CadenceFungibleTokenTutorialVault)

        log("Receiver参照が作成されました")
    }
    post {
        // Capabilityが正しく作成されたかどうかをチェック
        // force-unwrapし、capalibilyがない場合は実行が中断される
        getAccount(0x02).getCapability(/public/CadenceFungibleTokenTutorialReceiver)!
                    .check<&ExampleToken.Vault{ExampleToken.Receiver}>():
                    "Vault Receiver 参照は正しく作成されませんでした"
    }
}
```

### スクリプトでの確認

生成した Capability を使って Vault の残高を確認する。スクリプトを利用して実行する。

```cadence
import ExampleToken from 0x02

pub fun main() {
    let acct2 = getAccount(0x02)
    let acct2ReceiverRef = acct2.getCapability(/public/CadenceFungibleTokenTutorialReceiver)
                                .borrow<&ExampleToken.Vault{ExampleToken.Balance}>()
                                ?? panic("Could not borrow a reference to the acct2 receiver")
    log("Account 2 Balance")
	  log(acct2ReceiverRef.balance)
}
```

## token の mint

token を別のアカウント（0x03）で mint する。

### 事前準備

まずは 0x03 アカウントで Vault を扱えるように Vault リソースを定義する必要がある。

以下のトランザクションを 0x03 を署名者として実行する。

```cadence
// Setup Account
import ExampleToken from 0x02
// 0x02の署名なしでpubで定義しているメソッドを利用できる

transaction {
	prepare(acct: AuthAccount) {
		// tokenが空のVaultを生成
		let vaultA <- ExampleToken.createEmptyVault()

		// 0x03で署名して実行することでacctは0x03のメソッドになる
		acct.save<@ExampleToken.Vault>(<-vaultA, to: /storage/CadenceFungibleTokenTutorialVault)

    log("Empty Vault stored")

    // 生成したVaultへのCapabilityを生成する（ReceiverとBalanceインターフェースのみ）
		let ReceiverRef = acct.link<&ExampleToken.Vault{ExampleToken.Receiver, ExampleToken.Balance}>(/public/CadenceFungibleTokenTutorialReceiver, target: /storage/CadenceFungibleTokenTutorialVault)

    log("References created")
	}

    post {
        getAccount(0x03).getCapability<&ExampleToken.Vault{ExampleToken.Receiver}>(/public/CadenceFungibleTokenTutorialReceiver)
                        .check():
                        "Vault Receiver Reference was not created correctly"
    }
}
```

これで 0x03 アカウントのストレージには balance が 0 の Vault リソースが保存されている状態になった。

このように、アカウントは token を受け入れられるリソースを実行していない限り、どのような種類のトークンも受け取ることができない
つまり、誤って間違ったアドレスに token 送信することはできないため安全
→ 上記のトランザクションは誰でも実行できてしまうから、誰でも token を受け入れられる体制になってしまうんじゃない？

### mint の実行

以下のトランザクションを 0x02 を署名者として実行する。

```cadence
// Mint Tokens
import ExampleToken from 0x02

transaction {
  // transactionで利用する変数を型とともに定義
  // prepareで初期化必須となる
  let mintingRef: &ExampleToken.VaultMinter // let:不変
  var receiver: Capability<&ExampleToken.Vault{ExampleToken.Receiver}> // var:可変

  // 事前準備
	prepare(acct: AuthAccount) {
        // 0x02で署名しているため、0x02のprivateに格納されたCapabilityをborrowできる
        self.mintingRef = acct.borrow<&ExampleToken.VaultMinter>(from: /storage/CadenceFungibleTokenTutorialMinter)
            ?? panic("Could not borrow a reference to the minter")
        // 0x03のpublicなCapabilityを取得する
        let recipient = getAccount(0x03)
        self.receiver = recipient.getCapability<&ExampleToken.Vault{ExampleToken.Receiver}>(/public/CadenceFungibleTokenTutorialReceiver)
	}

  // 実行
  execute {
        // Mint 30 tokens and deposit them into the recipient's Vault
        self.mintingRef.mintTokens(amount: 30.0, recipient: self.receiver)
        log("30 tokens minted and deposited to account 0x03")
    }
}
```

前述のスクリプトを使って、0x02 アカウントと 0x03 アカウントに 30 ずつの token が保存されていることを確認できる。

## token の transfer

### 流れ

token をあるアカウント(0x03)から別のアカウント(0x02)に送信する。

token 送信の流れは

1. 0x03 の Vault リソースの withdraw()関数を使い、token を引き出し、一時的な Vault に格納する
2. 0x02 の Vault リソースの deposits()関数を使い、一時的な Vault にある token を 0x02 の Vault に格納する

コード例は以下。0x03 のトークンを送信するトランザクションなため、0x03 が署名して実行する必要がある。

```cadence
// Transfer Tokens
import ExampleToken from 0x02

transaction {
  // 一時的なVaultを格納するための変数
  var temporaryVault: @ExampleToken.Vault

  prepare(acct: AuthAccount) {
    // 0x03のvalutのrefをborrow
    let vaultRef = acct.borrow<&ExampleToken.Vault>(from: /storage/CadenceFungibleTokenTutorialVault)
        ?? panic("Could not borrow a reference to the owner's vault")

    self.temporaryVault <- vaultRef.withdraw(amount: 10.0) // temporaryVaultの初期化になる
  }

  execute {
    // 0x02のpublicなCapabilityを取得
    let recipient = getAccount(0x02)
    let receiverRef = recipient.getCapability(/public/CadenceFungibleTokenTutorialReceiver)
                      .borrow<&ExampleToken.Vault{ExampleToken.Receiver}>()
                      ?? panic("Could not borrow a reference to the receiver")

    // deposit実行（deposit関数内でリソースはdestroyされる）
    receiverRef.deposit(from: <-self.temporaryVault)
    log("Transfer succeeded!")
  }
}
```

- なぜわざわざ一時的な Vault `temporaryVault` を介して、関数を２回に分けて Transfer するのか
  - アカウントからアカウントにただ単に直接送信する場面は少なく、むしろ何かを購入した時などにトークンを移動する場面が多い
  - そういった時には、ストレージに直接送る前に、トランザクション内で一度 Vault として移動し、検証する必要がある
  - そのため、`temporaryVault`が使われている

前述のスクリプトを使って、0x02 アカウントと 0x03 アカウントの token が変更されていることを確認できる。
